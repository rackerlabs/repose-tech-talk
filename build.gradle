buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
    }
}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

task buildLogDirectory {
    doLast {
        file("$buildDir/logs").mkdirs()
    }
}

task buildDemoImage(type: DockerBuildImage) {
    inputDir = file('src/docker')
    tag = 'repose/demo'
}

task createDemoContainer(type: DockerCreateContainer) {
    dependsOn buildDemoImage, buildLogDirectory
    targetImageId { buildDemoImage.getImageId() }
    portBindings = ['8080:8080']
    binds = ["$projectDir/src/configs":"/etc/repose", "$buildDir/logs":"/var/log/repose"]
}

task startDemoContainer(type: DockerStartContainer) {
    dependsOn createDemoContainer
    targetContainerId { createDemoContainer.getContainerId() }
}
